xmlns:xml = "http://www.w3.org/XML/1998/namespace"

NL = [\r\n]
SP = [ ]
SPO = [ ]+ (MAX)
TWOSPORMORE = [ ]{2,}
SPO2 = [ ]*
ANYSP = [\r\n \t]*

BEGTRAN = "<T="
ENDTRAN = "=T>"
BEGP = "<="
ENDP = "=>"

BEGDIV = "<D="
ENDDIV = "=D>"

BEGOPT = "<:"
ENDOPT = ":>"

QUESTION = [\?]?

NUM = [0-9]+
LINENUM = [0-9]+[/\,a-zA-Z]*[ms0-9]*

TERMLANG = ((la)|(grc\-Latn))
FOREIGNLANG = ((la)|(grc\-Latn))
NOTELANG = ((en)|(de))


ANYLETTER = [^\^<>_#@~\[\]〚〛$\*\&\|\{\}\"¯≡+\u0323]

// WORD breaks at space and does not allow DOT
WORD = ([^ \/\\\n\r\t\^<>_#@~\[\]〚〛$\*\&\,\.\=\|\'\(\)\{\}\?\"¯≡+\u0323]+) (MAX)

// WORDDIV breaks at space and does not allow DOT - allows +, \,
WORDDIV = ([^ \n\r\t\^<>_#@~\[\]〚〛$\*\&\.\=\|\'\(\)\{\}\?\"¯≡\u0323]+) (MAX)

// WORDS allows space (therefore multiple words) and does not allow DOT
WORDS = ([^\/\\\n\r\t\^<>_#@~\[\]〚〛$\*\&\.\,\=\|\'\(\)\{\}\?\"¯≡+\u0323]+) (MAX)

//WORDSLR allows comma - used to make app lem BL work when lem resp= includes comma (ex. lem resp='cf. 5.11, 3.10')
WORDSLR = ([^\/\\\n\r\t\^<>_#@~\[\]〚〛$\*\&\=\|\'\(\)\{\}\?\"¯≡+\u0323]+) (MAX)

// WORDS
WORDSTERM = ([^\n\r\t\^<>#@~\[\]〚〛$\*\&\=\|\{\}¯≡+\u0323]+) (MAX)
//WORDSFOREIGN = WORDSTERM
WORDSFOREIGN = ([^\n\r\t\^<>#@~\[\]〚〛$\*\&\=\|\{\}¯≡+\u0323]+) (MAX)

//very permisive for free text
WORDSLOOSE = ([^\n\r\t\^<>#@~\[\]〚〛$\*\&\=\|\{\}¯≡+\u0323]+) (MAX)
WORDSANDMORE = ([^<>#@~\[\]〚〛$\*\&\=\|\{\}¯≡\u0323]+) (MAX)

file
    : [ANYSP s][transtag a][ANYSP ss] = <body>[ANYSP s][transtag a][ANYSP ss]</>
    : =

transtag
    :  [BEGTRAN] "." [WORDDIV d] [ANYSP s][divtag t][ANYSP ss] [ENDTRAN] = <div xml:lang=[WORDDIV d] type="translation" xml:space="preserve">[ANYSP s][divtag t][ANYSP ss]</>
    >: [BEGTRAN] "." [WORDDIV d] [ANYSP s][divtag t][ANYSP ss] [ENDTRAN] [ANYSP sss] [transtag more] = <div xml:lang=[WORDDIV d] type="translation" xml:space="preserve">[ANYSP s][divtag t][ANYSP ss]</> [ANYSP sss] [transtag more]

    :  [BEGTRAN] "." [WORDDIV d] [ANYSP s][ptag p][ANYSP ss] [ENDTRAN] = <div xml:lang=[WORDDIV d] type="translation" xml:space="preserve">[ANYSP s][ptag p][ANYSP ss]</>
    >: [BEGTRAN] "." [WORDDIV d] [ANYSP s][ptag p][ANYSP ss] [ENDTRAN] [ANYSP sss] [transtag more] = <div xml:lang=[WORDDIV d] type="translation" xml:space="preserve">[ANYSP s][ptag p][ANYSP ss]</> [ANYSP sss] [transtag more]


divtag
    : [BEGDIV] "." [WORDDIV d] " " [items a] [ENDDIV] = <div n=[WORDDIV d] type="textpart">[items a]</>
    >: [BEGDIV] "." [WORDDIV n] "." [WORDDIV s] " " [items a] [ENDDIV] = <div n=[WORDDIV n] subtype =[WORDDIV s] type="textpart">[items a]</>

    >: [BEGDIV] " " [items p] " " [ENDDIV] = <div type="textpart">[items p]</>

    >: [BEGDIV] "." [WORDDIV d] " " [ANYSP s][ptag p][ANYSP ss] [ENDDIV] = <div n=[WORDDIV d] type="textpart">[ANYSP s][ptag p][ANYSP ss]</>
    >: [BEGDIV] "." [WORDDIV d] " " [ANYSP s][ptag p][ANYSP ss] [ENDDIV] [ANYSP sss] [divtag more] = <div n=[WORDDIV d] type="textpart">[ANYSP s][ptag p][ANYSP ss]</> [ANYSP sss][divtag more]
    >: [BEGDIV] "." [WORDDIV n] "." [WORDDIV s] " " [ANYSP ss][ptag p][ANYSP sss] [ENDDIV] = <div n=[WORDDIV n] subtype =[WORDDIV s] type="textpart">[ANYSP ss][ptag p][ANYSP sss]</>
    >: [BEGDIV] "." [WORDDIV n] "." [WORDDIV s] " " [ANYSP ss][ptag p][ANYSP sss] [ENDDIV] [ANYSP ssss] [divtag more] = <div n=[WORDDIV n] subtype =[WORDDIV s] type="textpart">[ANYSP ss][ptag p][ANYSP sss]</> [ANYSP ssss][divtag more]

    >: [BEGDIV] "." [WORDDIV d] " " [ANYSP s][divtag t][ANYSP ss] [ENDDIV] = <div n=[WORDDIV d] type="textpart">[ANYSP s][divtag t][ANYSP ss]</>
    >: [BEGDIV] "." [WORDDIV d] " " [ANYSP s][divtag t][ANYSP ss] [ENDDIV] [ANYSP sss] [divtag more] = <div n=[WORDDIV d] type="textpart">[ANYSP s][divtag t][ANYSP ss]</> [ANYSP sss][divtag more]
    >: [BEGDIV] "." [WORDDIV n] "." [WORDDIV s] " " [ANYSP ss][divtag t][ANYSP sss] [ENDDIV] = <div n=[WORDDIV n] subtype =[WORDDIV s] type="textpart">[ANYSP ss][divtag t][ANYSP sss]</>
    >: [BEGDIV] "." [WORDDIV n] "." [WORDDIV s] " " [ANYSP ss][divtag t][ANYSP sss] [ENDDIV] [ANYSP ssss] [divtag more] = <div n=[WORDDIV n] subtype =[WORDDIV s] type="textpart">[ANYSP ss][divtag t][ANYSP sss]</> [ANYSP ssss][divtag more]

// this is equivalent to <ab> in normal L+
ptag
    :  [BEGP] [divtag d] [ENDP] = <p>[divtag d]</>
    :  [BEGP] [items z] [ENDP] = <p>[items z]</>
    >: [BEGP] [items z] [ENDP] [ptag more] = <p>[items z]</> [ptag more]
    >: [BEGP] [items z] [ENDP] [ANYSP sp] [ptag more] = <p>[items z]</> [ANYSP sp] [ptag more]
    >: [BEGP] [ENDP] = <p></>

items
    :  [item i] [items more] = [item i] [items more]
    >: [item p] = [item p]

item
    : [app a] = [app a]
    : [foreign f] = [foreign f]
    : [linenumber l] = [linenumber l]
    : [del d] = [del d]
    : [term t] = [term t]
    : [lb l] = [lb l]
    : [gap g] = [gap g]
    : [vac v] = [vac v]
    : [meta n] = [meta n]
    : [note n] = [note n]
    : [choice c] = [choice c]
    : [supplied s] = [supplied s]

    : [NL n] = [NL n]
    >: [WORDS w] = [WORDS w]
    >: [ANYLETTER a] = [ANYLETTER a]

segs
    : [seg s] "|" [segs more] = [seg s] [segs more]
    >: [seg sg] = [seg sg]

seg
    : [items i] = <seg>[items i]</>

linenumber
    :  "((" [LINENUM n] "))" = <milestone unit="line" n=[LINENUM n]/>
    >: "(((" [LINENUM n] ")))" = <milestone unit="line" n=[LINENUM n] rend="break"/>

foreign
    : "~|" [items a] "|~" [FOREIGNLANG lang] " " = <foreign xml:lang=[FOREIGNLANG lang]>[items a]</>
    : "~|" [WORDSFOREIGN a] "|~" [FOREIGNLANG lang] " " = <foreign xml:lang=[FOREIGNLANG lang]>[WORDSFOREIGN a]</>

lb
    : [NL] [TWOSPORMORE s] = [NL] [TWOSPORMORE s] <lb></>
    >: [NL] [NL] = [NL]<lb></>
    >: [NL] [NL] = <lb></>

term
    : "<" [items def] "=" [WORDSTERM t] ">" = <term target=[WORDSTERM t]>[items def]</>
    : "<" [items def] "~" [TERMLANG lang] "=" [WORDSTERM t] ">" = <term target=[WORDSTERM t] xml:lang=[TERMLANG lang]>[items def]</>

    >: "<" [WORDSTERM def] "=" [WORDSTERM t] ">" = <term target=[WORDSTERM t]>[WORDSTERM def]</>
    >: "<" [WORDSTERM def] "~" [TERMLANG lang]  "=" [WORDSTERM t] ">" = <term target=[WORDSTERM t] xml:lang=[TERMLANG lang]>[WORDSTERM def]</>

gap
    : "[...]" = <gap reason="lost" extent="unknown" unit="character"></>
    : "..." = <gap reason="illegible" extent="unknown" unit="character"></>

vac
    : "vac." [NUM n] = <space quantity=[NUM n] unit="character" precision="low"></>

del
    : "〚" [WORDSLOOSE w] "〛" = <del>[WORDSLOOSE w]</>
    >: "〚" [items a] "〛" = <del>[items a]</>

supplied
    : "[" [WORDSLOOSE w] "]" = <supplied reason="lost">[WORDSLOOSE w]</>
    : "[" [WORD w] "]" = <supplied reason="lost">[WORD w]</>
    >: "[" [items i] "]" = <supplied reason="lost">[items i]</>

choice
    : [BEGOPT] [WORDSLOOSE w] "|" [segs b] [ENDOPT] = <choice><seg>[WORDSLOOSE w]</>[segs b]</>
    >: [BEGOPT] [items i] "|" [segs b] [ENDOPT] = <choice><seg>[items i]</>[segs b]</>

app
    : [BEGOPT] [items a] "|:" [WORDSLOOSE c] "|" [ENDOPT] = <app><lem resp=[WORDSLOOSE c]>[items a]</></>
    : [BEGOPT] [items a] "|:|" [ENDOPT] = <app><lem>[items a]</></>

    : [BEGOPT] [WORDSLOOSE a] "|:" [WORDSLOOSE c] "|" [ENDOPT] = <app><lem resp=[WORDSLOOSE c]>[WORDSLOOSE a]</></>
    : [BEGOPT] [WORDSLOOSE a] "|:|" [ENDOPT] = <app><lem>[WORDSLOOSE a]</></>

    : [BEGOPT] [items a] "|BGU:" [WORDSLR c] "|" [ENDOPT] = <app type="BGU"><lem resp=[WORDSLR c]>[items a]</></>
    : [BEGOPT] [items a] "|BGU:|" [ENDOPT] = <app type="BGU"><lem>[items a]</></>

    : [BEGOPT] [WORDSLOOSE a] "|BGU:" [WORDSLOOSE c] "|" [ENDOPT] = <app type="BGU"><lem resp=[WORDSLOOSE c]>[WORDSLOOSE a]</></>
    : [BEGOPT] [WORDSLOOSE a] "|BGU:|" [ENDOPT] = <app type="BGU"><lem>[WORDSLOOSE a]</></>

    : [BEGOPT] [items a] "|BGU_DDbDP:" [WORDSLOOSE c] "|" [ENDOPT] = <app type="BGU_DDbDP"><lem resp=[WORDSLOOSE c]>[items a]</></>
    : [BEGOPT] [items a] "|BGU_DDbDP:|" [ENDOPT] = <app type="BGU_DDbDP"><lem>[items a]</></>

    : [BEGOPT] [WORDSLOOSE a] "|BGU_DDbDP:" [WORDSLOOSE c] "|" [ENDOPT] = <app type="BGU_DDbDP"><lem resp=[WORDSLOOSE c]>[WORDSLOOSE a]</></>
    : [BEGOPT] [WORDSLOOSE a] "|BGU_DDbDP:|" [ENDOPT] = <app type="BGU_DDbDP"><lem>[WORDSLOOSE a]</></>

pleiades_url
    : [WORDSLOOSE url] = "http://pleiades.stoa.org/places/"[WORDSLOOSE url]

lgpn_url
    : [WORDSLOOSE url] = "http://www.lgpn.ox.ac.uk/id/"[WORDSLOOSE url]

meta
    : "<+" "place=" [WORDSLOOSE url] "|" [WORD w] "+>" = <placeName ref=[WORDSLOOSE url]>[WORD w]</>
    : "<+" "geog=" [WORDSLOOSE url] "|" [WORD w] "+>" = <geogName ref=[WORDSLOOSE url]>[WORD w]</>
    : "<+" "pleiades=" [pleiades_url url] "|" [WORD w] "+>" = <placeName ref=[pleiades_url url]>[WORD w]</>
    : "<+" "geog_pleiades=" [pleiades_url url] "|" [WORD w] "+>" = <geogName ref=[pleiades_url url]>[WORD w]</>
    : "<+" "person=" [WORDSLOOSE url] "|" [WORD w] "+>" = <persName ref=[WORDSLOOSE url]>[WORD w]</>
    : "<+" "lgpn=" [lgpn_url url] "|" [WORD w] "+>" = <persName ref=[lgpn_url url]>[WORD w]</>

    >: "<+" "place=" [WORDSLOOSE url] "|" [items i] "+>" = <placeName ref=[WORDSLOOSE url]>[items i]</>
    >: "<+" "geog=" [WORDSLOOSE url] "|" [items i] "+>" = <geogName ref=[WORDSLOOSE url]>[items i]</>
    >: "<+" "pleiades=" [pleiades_url url] "|" [items i] "+>" = <placeName ref=[pleiades_url url]>[items i]</>
    >: "<+" "geog_pleiades=" [pleiades_url url] "|" [items i] "+>" = <geogName ref=[pleiades_url url]>[items i]</>
    >: "<+" "person=" [WORDSLOOSE url] "|" [items i] "+>" = <persName ref=[WORDSLOOSE url]>[items i]</>
    >: "<+" "lgpn=" [lgpn_url url] "|" [items i] "+>" = <persName ref=[lgpn_url url]>[items i]</>

note
    : "\/*" [WORD a] "|" [WORDSANDMORE more] "*\/" = <note type=[WORD a]>[WORDSANDMORE more]</>
    : "\/*" [WORDSANDMORE a] "*\/" = <note>[WORDSANDMORE a]</>
